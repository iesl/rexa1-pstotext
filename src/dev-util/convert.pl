#!/usr/bin/perl -w

# testing script that runs pstotext on the specified ps/pdf file; runs
# pstotext twice: once with ligature correction enabled, once with it
# disabled, allowing manual comparison of the effects; XML output of
# pstotext is converted to ASCII output; output files are written with
# ".txt" and ".lig.txt" suffixes.
#
# also supports regression testing, by comparing the converted output
# to a *.lig.txt file that was generated by this script at an earlier
# point in time; use the --regression-test-dir option to specify the
# directory containing this earlier version of the output file

use strict;
use File::Basename;

use Getopt::Long;

my $usage = "usage: $0 [--regression-test-dir <dir>] {--document <file> | <file>} {--output-dir <dir>}\n";
my $file;
my $output_dir;
my $regression_test_dir;
my $fix_ligatures = 0;
my $keep_xml_file = 0;
my $run_ps2ascii = 0; # whether to run ps2ascii, for the purpose of
                      # comparing our pstotext system to another
                      # popular system
my $gs_executable = "";
my $debug_pstotext;
my $pstotextRoot = dirname( $0 );

my $opt_result = GetOptions( "regression-test-dir|regression-dir|reg-dir=s" => \$regression_test_dir,
                             "document|file=s" => \$file,
                             "output-dir=s" => \$output_dir,
                             "fix-ligatures|ligatures!" => \$fix_ligatures,
                             "run-ps2ascii|ps2ascii!" => \$run_ps2ascii,
                             "keep-xml-file|xml!" => \$keep_xml_file,
                             "gs|ghostscript=s" => \$gs_executable,
                             "debug-pstotext!" => \$debug_pstotext );
$file = shift @ARGV unless $file;
die "$usage" unless $opt_result;
die "invalid document file '$file',  in --document option"
  unless -f $file;
die "invalid directory '$regression_test_dir', in --regression-test-dir option"
  unless !$regression_test_dir || -d $regression_test_dir;
$output_dir = dirname( $file ) unless $output_dir;

my $file_base = basename( $file );
my $output_file = "$output_dir/$file_base";
my $xml_file = "$output_file.xml";
my $lig_dictionary = "$pstotextRoot/../ligatures.txt";
my $pstotext_exec = "$pstotextRoot/../pstotext " .
  "-output $xml_file " .
  ( $debug_pstotext ? "-debug " : "" ) .
  ( $gs_executable ? "-gs $gs_executable " : "" );
my $xml2txt_exec = "$pstotextRoot/xml2txt.pl";

print "processing $file...\n";

if ( $fix_ligatures ) {
  system( "$pstotext_exec -ligatures $lig_dictionary $file" );
  system( "$xml2txt_exec $xml_file > $output_file.lig.txt" );
} else {
  system( "$pstotext_exec $file" );
  system( "$xml2txt_exec $xml_file > $output_file.txt" );
}
unlink $xml_file unless $keep_xml_file;

system( "ps2ascii $file $output_file.ps2ascii.txt") if $run_ps2ascii;

if ( $regression_test_dir ) {
  my $diff_result = system( "diff $regression_test_dir/$file_base.lig.txt $output_file.lig.txt" ) >> 8;
  if ( $diff_result ) {
    print STDERR "regression test failure: '$regression_test_dir/$file_base' and '$output_file' differ\n";
    exit 1;
  }
}

exit 0;
